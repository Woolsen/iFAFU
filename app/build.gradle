plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

def keystoreDebugProperties = new Properties()
def keystoreDebugPropertiesFile = rootProject.file('key-debug.properties')

if (keystoreDebugPropertiesFile.exists()) {
    keystoreDebugProperties.load(new FileInputStream(keystoreDebugPropertiesFile))
}

def keystoreReleaseProperties = new Properties()
def keystoreReleasePropertiesFile = rootProject.file('key-release.properties')

if (keystoreReleasePropertiesFile.exists()) {
    keystoreReleaseProperties.load(new FileInputStream(keystoreReleasePropertiesFile))
}

android {

    compileSdk 31

    defaultConfig {
        applicationId "cn.ifafu.ifafu"
        minSdk 21
        targetSdk 31
        versionCode 305
        versionName "1.4.10"
        resConfigs "zh"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    signingConfigs {
        debug {
            keyAlias keystoreDebugProperties['keyAlias']
            keyPassword keystoreDebugProperties['keyPassword']
            storeFile file(keystoreDebugProperties['storeFile'])
            storePassword keystoreDebugProperties['storePassword']
        }
        release {
            keyAlias keystoreReleaseProperties['keyAlias']
            keyPassword keystoreReleaseProperties['keyPassword']
            storeFile file(keystoreReleaseProperties['storeFile'])
            storePassword keystoreReleaseProperties['storePassword']
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            shrinkResources false
            //为debug版本的包名添加.debug后缀
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    app_name      : "iFAFU测试版",
                    app_icon      : "@mipmap/ic_launcher_debug",
                    app_icon_round: "@mipmap/ic_launcher_round_debug",
            ]
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    app_name      : "@string/app_name",
                    app_icon      : "@mipmap/ic_launcher",
                    app_icon_round: "@mipmap/ic_launcher_round",
            ]
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "iFAFU_v${defaultConfig.versionName}(build_${defaultConfig.versionCode}_${variant.buildType.name}).apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
        useIR = true
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation 'com.google.android.material:material:1.5.0'
//    implementation 'com.tencent.tinker:tinker-android-lib:1.9.9'

    //https://developer.android.com/training/dependency-injection/hilt-android
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    //https://developer.android.com/training/data-storage/room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //https://developer.android.com/topic/libraries/architecture/lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"

    //https://developer.android.com/topic/libraries/architecture/paging/v3-overview
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"

    //https://developer.android.com/guide/navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.1"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.contrarywind:Android-PickerView:4.1.9"
    implementation "com.github.ybq:Android-SpinKit:1.4.0"
    implementation "com.github.Chrisvin:RubberPicker:1.5"
    implementation "com.github.warkiz.widget:indicatorseekbar:2.1.2"
    implementation "com.github.chrisbanes:PhotoView:2.3.0"
    implementation "com.afollestad.material-dialogs:core:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:color:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:datetime:$material_dialogs_version"
    // 网络框架
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "org.jsoup:jsoup:1.14.3"
    implementation 'com.alibaba:fastjson:1.2.79'

    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4"
    implementation "me.drakeet.multitype:multitype:4.0.0-alpha3"
    implementation "com.gyf.immersionbar:immersionbar:3.0.0"
    implementation "com.jaeger.statusbarutil:library:1.5.1"
    implementation "com.nineoldandroids:library:2.4.0"
    implementation "cat.ereza:customactivityoncrash:2.3.0"
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.blankj:utilcodex:1.31.0"
    implementation "com.tencent.bugly:crashreport_upgrade:1.5.23"
    implementation 'com.github.yalantis:ucrop:2.2.8'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
    //http://www.dokit.cn/
    def dokit_version = "3.4.3"
    debugImplementation "io.github.didi.dokit:dokitx:$dokit_version"
    releaseImplementation "io.github.didi.dokit:dokitx-no-op:$dokit_version"

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "androidx.test:core:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}

kapt {
    correctErrorTypes true
}